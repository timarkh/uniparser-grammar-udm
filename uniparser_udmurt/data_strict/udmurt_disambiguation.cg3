DELIMITERS = "<SENT_BOUNDARY>";

LIST NegVerbs = "уг" ("уд" V) "уз" ("ум" V) "ӧй" ("ӧд" V) "ӧз" ("ӧм" V);
LIST NegVerbs1 = ("ӧм" V) "ӧй";
LIST NegVerbs2 = ("уд" V) ("ӧд" V);
LIST NegVerbs3 = "уз" ("ум" V) "ӧз" ("ӧм" V);
LIST NegVerbsPst = ("ӧм" V) "ӧй" ("ӧд" V) "ӧз";
LIST NegVerbsNpst = "уг" ("уд" V) "уз" ("ум" V);
LIST Clitics = "но" "ни" "ини" "инь" "на" "пе" "пӧй" "а" "о" "ук" "ик" "ке" "ма" "гинэ" "гынэ" "гынэк" "лэся" "вылды";
LIST VerbalClitics = "ни" "ини" "инь" "на" "ук" "лэся" "вылды";
SET NotClitics = (*) - Clitics;
LIST Poss = 1sg 2sg 3sg 1pl 2pl 3pl poss_comp;
LIST PossNot3sg = 1sg 2sg 1pl 2pl 3pl;
SET NN = (N sg nom) - Poss;
SET NomPoss = (N sg nom 1sg) OR (N sg nom 2sg) OR (N sg nom 3sg) OR (N sg nom 1pl) OR (N sg nom 2pl) OR (N sg nom 3pl);
LIST DemPersPro = "<со>" "<та>";
LIST NAdj = N A;
LIST VPred = V PREDIC;
SET VFinPred = (V res) OR (V deb) OR (V 1) OR (V 2) OR (V 3) OR (PREDIC);
SET VTr1 = (V tr) OR (V caus);
SET VTr2 = VTr1 - (pass);
SET VTr = VTr2 OR ("вераны") OR ("шуккыны") OR ("пырыны") OR ("вазьыны") OR ("оскыны");
LIST BOS = (>>>);
SET BOSPunct = (punct) | BOS;
LIST WhQuestions = "кызьы" "ку" "кӧня" "кытын" "кытысь" "кытысен" "кытӥ" "кытчы";



SECTION    # Verbs
# Only leave V,neg analyses if there is a negative verb nearby.
REMOVE (V neg) IF (NEGATE *-1 NegVerbs BARRIER NotClitics) 
                  (NEGATE 0 NegVerbs);
SELECT (V neg) IF (*-1 NegVerbs BARRIER NotClitics) ;

# Choose correct person for negated verbs based on the
# negative verb.
SELECT (V neg 3) IF (*-1 NegVerbs3 BARRIER NotClitics);
REMOVE (V neg 3) IF (*-1 NegVerbs2 BARRIER NotClitics);
REMOVE (V neg 2) IF (*-1 ("<уг>"i) BARRIER NotClitics);
REMOVE (V neg 1) IF (*-1 NegVerbs2 BARRIER NotClitics);
SELECT (V neg 1) IF (*-1 NegVerbs1 BARRIER NotClitics);
SELECT (V neg pst) IF (*-1 NegVerbsPst BARRIER NotClitics);
REMOVE (V neg pst) IF (*-1 NegVerbsNpst BARRIER NotClitics);
REMOVE (V neg pst 1) IF (0 ("<ӧй>"i))
                        (NEGATE *0 ("<вал>"i) BARRIER NotClitics);
SELECT (V neg pst 1) IF (0 ("<ӧй>"i))
                        (*0 ("<вал>"i) BARRIER NotClitics);

# Pst2 vs. nominalization in -m
REMOVE (V evid sg) IF (1 ("бер" ill));
REMOVE (V ptcp pst sg nom) IF (1 ("шуыса") OR ("шуса") OR VerbalClitics);
SELECT (V evid 3 sg) IF (0 (ptcp 3sg sg nom))
                        (*1 ("потыны") BARRIER NotClitics);

# fut,1,sg vs. prs,3,pl: look at possible subjects
SELECT (V fut 1 sg) IF (NOT 0 (V pass))
                       (*-1 ("<мон>"i) BARRIER BOSPunct | (PRO pers sg nom) | (N sg nom));
SELECT (V prs 3 pl) IF (NOT 0 (V pass))
                       (*-1 (N pl nom) | ("<ваньзы>"i) BARRIER BOSPunct | (PRO pers sg nom) | (N sg nom));
SELECT (V prs 3 pl) IF (NOT 0 (V pass))
                       (*-1 (PRO pers pl nom) BARRIER BOSPunct | (PRO pers sg nom) | (N sg nom));
SELECT (V prs 3 pl) IF (NOT 0 (V pass))
                       (1C (PRO pers pl nom) OR (N pl nom))
                       (-1 BOSPunct OR (CNJ));
SELECT (V prs 3 pl) IF (-2C (V prs 3 pl))
                       (-1 (","));
SELECT (V 3 pl) IF (0 (V pass))
                   (-1C (N pl nom) | (PRO pl nom) | ("<ваньзы>"i));
REMOVE (V fut pass) IF (0 (V prs))
                       (NOT 0 (V caus))
                       (-1C (acc))
                       (*-2 BOSPunct OR (CNJ) BARRIER NotClitics);
REMOVE (V fut pass) IF (0 (V prs))
                       (NOT 0 (V caus))
                       (1C (acc))
                       (*-1 BOSPunct OR (CNJ) BARRIER NotClitics)
                       (*2 BOSPunct OR (CNJ) BARRIER NotClitics);

# Remove 1sg analysis for evidential -em if not followed
# by val
REMOVE (V evid 1 sg) IF (0 (".*(([^к]|[^ь]к)ем|[эая]м)$"r))
                        (NEGATE *1 ("вал"i) BARRIER NotClitics);

# imperative plural is a very rare form which coincides
# with prs,3,pl
REMOVE (V imp pl) IF (-1 (PRO sg nom) OR WhQuestions);
REMOVE (V imp pl) IF (0 (V intr pass 3 sg));

# kučʼkənə can not be the caustive-detransitive for kuənə
REMOVE ("куыны") IF (0 ("кутскыны"));

# Forms in -ono should only be analysed as debitive, not
# as nominalization in -n plus an attributive (which is
# probably the diachronic source of the former).
REMOVE (V vn attr_o);


# imperative vs. prs.3sg
REMOVE (V imp) IF (0 (V prs 3 sg))
                  (0C (V))
                  (*-1 NomPoss BARRIER NotClitics - (ADV))
                  (NEGATE *-1 (acc) BARRIER NotClitics - (ADV))
                  (NEGATE -2 (V 3 sg) OR (V res) OR (PRED));
REMOVE (V imp) IF (0 (V prs 3 sg))
                  (0C (V intr) - (caus))
                  (*-1 (N sg nom) - NomPoss BARRIER NotClitics - (ADV))
                  (NEGATE *0 (V tr) BARRIER BOSPunct);
REMOVE (V imp) IF (0 (V prs 3 sg))
                  (0C ("потыны"i) OR ("кельшыны"i))
                  (*-1C (N dat) OR (PRO dat) BARRIER BOSPunct OR VPred);


SECTION    # Nouns
# Illative vs. 1sg
# If there is a possessor to the left and the noun is animate,
# it could hardly be in the illative.
REMOVE (N ill sg) IF (0 (N anim 1sg))
                     (-1 ("<мынам>"i))
                     (NOT 0 (N ill Poss));
# If the noun is animate and there is no possessor to the left,
# but no verb requiring the illative either (but there are other verbs),
# it still could hardly be in the illative.
REMOVE (N ill sg) IF (0 (N anim 1sg))
                     (NEGATE 0*C (V with_ill) BARRIER BOS)
                     (0*C VPred BARRIER BOS);
# Time measurement units and months are usually in the illative.
REMOVE (N nom sg 1sg) IF (0 (N time_meas sg ill))
                         (NEGATE *-1 ("<мынам>"i) BARRIER BOSPunct);
# Exclamations starting with "e" or "oj" are in nom.1sg.
REMOVE (N sg ill) IF (0 (N nom sg 1sg))
                     (-1 ("<э>"i) OR ("<ой>"i) OR ("<эй>"i));
# Some constructions require a participle with possessives.
REMOVE (sg ill) IF (0 (V ptcp pst sg 1sg))
                   (*1 ("<потэ>"i) OR ("<потӥз>"i) OR ("<вань>"i) OR ("<ӧвӧл>"i) BARRIER NotClitics);
REMOVE (sg ill) IF (0 (V ptcp pst sg 1sg))
                   (*-1 ("<потэ>"i) OR ("<потӥз>"i) OR ("<вань>"i) OR ("<ӧвӧл>"i) BARRIER NotClitics - (PRO sg gen) - ("<->"));
# Dialectal hortative in -оме
REMOVE (V hort) IF (0 (V ptcp pst sg 1sg))
                   (*1 ("<потэ>"i) OR ("<потӥз>"i) OR ("<вань>"i) OR ("<ӧвӧл>"i) BARRIER NotClitics);
REMOVE (V hort) IF (0 (V ptcp pst sg 1sg))
                   (*-1 ("<потэ>"i) OR ("<потӥз>"i) OR ("<вань>"i) OR ("<ӧвӧл>"i) BARRIER NotClitics - (PRO sg gen) - ("<->"));
REMOVE (evid 2 sg) IF (0 (V ptcp pst sg 2sg))
                      (*1 ("<потэ>"i) OR ("<потӥз>"i) OR ("<вань>"i) OR ("<ӧвӧл>"i) BARRIER NotClitics);
REMOVE (evid 2 sg) IF (0 (V ptcp pst sg 2sg))
                      (*-1 ("<потэ>"i) OR ("<потӥз>"i) OR ("<вань>"i) OR ("<ӧвӧл>"i) BARRIER NotClitics - (PRO sg gen) - ("<->"));

# If there is a verb that requires illative and no "mənam" around,
# this is also probably the illative.
REMOVE (N nom sg 1sg) IF (0 (N sg ill))
                         (NEGATE *0 ("<мынам>"i) BARRIER BOSPunct)
                         (*0 (V with_ill) BARRIER BOSPunct);
# In the opposite case, it is probably nom.1sg.
REMOVE (N sg ill) IF (0 (N nom sg 1sg))
                     (*0 ("<мынам>"i) BARRIER BOSPunct)
                     (NEGATE *0 (V with_ill) BARRIER BOS);
# Couple of cases that are almost never either of the two.
REMOVE (N sg ill) IF (0 ("<номыре>") OR ("<нокинэ>") OR ("<нокине>") OR ("<гынэ>"))
                     (NEGATE *0 ("<мынам>"i) BARRIER BOSPunct)
                     (NEGATE *0 (V with_ill) BARRIER BOS);
REMOVE (N nom sg 1sg) IF (0 ("<номыре>") OR ("<нокинэ>") OR ("<нокине>") OR ("<гынэ>"))
                         (NEGATE *0 ("<мынам>"i) BARRIER BOSPunct)
                         (NEGATE *0 (V with_ill) BARRIER BOS);

# ill vs. prs.3sg of similar verbs
REMOVE (N) IF (0 ("<зоре>"i) OR ("<вае>"i) OR ("<возе>"i) OR ("<шуэ>"i))
              (NEGATE *0 VPred BARRIER BOS);
REMOVE (N) IF (0 ("<вуэ>"i))
              (NEGATE *0 VPred BARRIER BOS)
              (*0 (ill) BARRIER BOS);
# gəne is usually PART
REMOVE (N) IF (0 ("<гынэ>"i))
              (-1 (NUM) OR ("<каллен>"i) OR ("<озь>"i) OR ("<ыжыт>"i));

# Accusative vs. 3sg
# Choose 3sg if there is a genitive possessor to the left.
SELECT (N sg nom 3sg) IF (0 (N sg acc))
                         (-1C (N gen) OR (PRO gen) OR (vn gen))
                         (NOT 1 (V tr));
# Remove acc in -ez if there are no transitive verbs but there is
# an intransitive verb or predicative.
REMOVE (N sg acc) IF (0 ("<.*[еэ]з>"r))
                     (0 (N sg nom 3sg))
                     (NEGATE *0 VTr BARRIER BOS)
                     (*0 (V intr) - (V ptcp prs) OR (PREDIC));

# Remove pl.nom.2sg in -tə if there is a transitive verb
# around and no other accusative candidates.
REMOVE (N pl nom 2pl) IF (0 (N pl acc))
                         (*0 VTr BARRIER BOS)
                         (NEGATE *0 (acc));
# Soostə etc. can hardly ever be pl.nom.2sg in real life.
REMOVE (PRO pl nom 2pl) IF (0 ("<соосты>i") OR ("<таосты>"i) OR ("<асместы>"i) OR ("<асьместы>"i));
REMOVE (PRO pl nom 2pl) IF (0 ("<вылды>i"))
                           (1 (punct));

# Proper nouns vs. non-proper: look if capitalized
REMOVE (N PN) IF (0 (N) - (PN))
                 (0 ("<[^А-ЯЁӞӜӴӦA-Z0-9].*>"r));


SECTION    # Adjectives
# Most adjectives can also work as adverbs. If an adjective
# precedes a noun, it is not an adverb. If an adverb precedes
# a verb, it is not an adjective.
REMOVE (ADV) IF (0 (ADJ))
                (*1C (N) BARRIER (*) - (ADJ));
REMOVE (A) IF (0 (ADV))
              (*1C (V) - ("шуыны") - ("вераны") - ("луыны") BARRIER (*) - (ADV));
REMOVE (N) IF (-1 ("<туж>"i))
              (0 (ADV) OR (ADJ))
              (0 (N));

# adj_nmlz actually happens very infrequently
REMOVE (adj_nmlz) IF (0 (attr_o adj_nmlz))
                     (0 (V 3 pl) OR (V fut 1 sg));



SECTION    # Numerals
REMOVE (nom) IF (0 (NUM nom) - Poss - (sg) - (pl));
REMOVE (acc) IF (0 (NUM nom 3sg) OR (NUM nom poss_comp));

SECTION    # Pronouns

# Negative pronouns vs. nomər "worm"
SELECT (PRO) IF (0 ("<номыре>"i))
                (1 ("<но>"i));
SELECT (PRO) IF (0 ("номыр"))
                (1 ("<но>"i))
                (*0 (V neg) BARRIER BOS);

SECTION    # Unchangeable POS

# Russian conjunctions vs. native particles.
SELECT (rus) IF (0 ("а"))
                (-1 BOSPunct);
SELECT (rus) IF (0 ("а"))
                (*-1 BOS BARRIER (*) - (punct));
REMOVE (rus) IF (0 ("а"))
                (NOT -1 BOSPunct);
REMOVE (PART rus) IF (0 ("а"))
                     (NOT 1 BOSPunct);
SELECT (CNJ rus) IF (0 ("но"))
                    (-1 BOSPunct);
SELECT (CNJ rus) IF (0 ("но"))
                    (*-1 BOS BARRIER (*) - (punct));
REMOVE (CNJ rus) IF (0 ("но"))
                    (NOT -1 BOSPunct);

# med V.fut
SELECT (PART) IF (0 ("<мед>"i))
                 (1 (V fut));



SECTION    # Other inter-POS homonymy


# kare: city-ill vs. do-prs.3sg
SELECT (V) IF (0 ("<каре>"i) OR ("<кар>"i))
              (-1 ("марым"i) OR (V rus_inf) OR ("<эн>"i) OR ("<тау>"i) OR ("<бакель>"i) OR ("<котьмар>"i) OR ("<котьма>"i) OR ("<оломар>"i));
SELECT (N) IF (0 ("<каре>"i) OR ("<кар>"i))
              (-1 ("<кузьыли>"i) OR (N topn) OR ("<дуринчи>"i) OR ("<муш>"i) OR ("<пуны>"i));


# ule: down-ill vs. live-prs.3sg
SELECT (V) IF (0 ("<улэ>"i))
              (*-1 (loc) BARRIER NotClitics);
SELECT (N) IF (0 ("<улэ>"i))
              (-1 (N nom))
              (*1 (V) BARRIER NotClitics);


# vožʼ: meadow vs. hold.IMP
SELECT (N) IF (0 ("<возь>"i))
              (1 ("выл") OR ("дор") OR ("дур"));
SELECT (V) IF (0 ("<возь>"i))
              (-1C (acc))
              (NEGATE *1 (V) BARRIER BOSPunct);


# val: to be vs. horse
SELECT (V) IF (0 ("<вал>"i))
              (*-1 ("ӧй") BARRIER NotClitics);
SELECT (V) IF (0 ("<вал>"i))
              (*-1 (loc) BARRIER NotClitics)
              (NOT 1 (V) OR (PRED));
SELECT (V) IF (0 ("<вал>"i))
              (1 VerbalClitics)
              (NOT 2 (V));

# ber: N vs. A/ADV
SELECT (N) IF (0 ("<бер>"i))
              (1 ("пал"));

# vitʼe: five-ill vs. wait-prs.3sg
SELECT (V) IF (0 ("<вите>"i))
              (1 VerbalClitics);

# vitʼ: five vs. wait.imp.2sg
SELECT (NUM) IF (0 ("<вить>"i))
                (-1 (NUM));
SELECT (NUM) IF (0 ("<вить>"i))
                (1 (time_meas) OR ("километр") OR ("килограмм") OR ("грамм") OR ("метр") OR ("сантиметр"));

# vera: N vs. V
REMOVE (N) IF (0 ("вера"i))
              (0 (V))
              (*0 (N acc) BARRIER NotClitics);

# kijaz[ə]: "in the hand" vs. "broke"
REMOVE (V) IF (0 ("<кия[мдз]ы?>"r))
              (*-1 ("паллян") OR ("бур") OR ("мукет") BARRIER NotClitics);
REMOVE ("кий") IF (0 ("<ки(е|ын|я[мдз]ы?)>"r))
                  (*-1 ("паллян") OR ("бур") OR ("мукет") BARRIER NotClitics);

# pənə: dog
SELECT (N) IF (0 ("<пуны>"i) OR ("<пыны>"i))
              (NOT 0 (V neg));

# Алла: name vs. interjection
SELECT (INTRJ) IF (0 ("<алла>"i))
                  (-1 ("<э>"i) OR ("<ой>"i) OR ("<эй>"i));
SELECT (N persn) IF (0 ("<алла>"i))
                    (NOT -1 ("<э>"i) OR ("<ой>"i) OR ("<эй>"i));

# шуыны: "сказать" vs. "вять"
SELECT (V tr) IF (0 ("шуыны"i))
                 (0 (V 1) OR (V deb));

# кызь: 20 vs. "urine"
SELECT (NUM) IF (0 ("<кызь>"i) OR ("<кызьгес>"i))
                (1 (NUM) OR (N nom));

# кыз: "fir" vs. "urinate"
SELECT (N) IF (0 ("<кыз>"i) OR ("<кызэ>"i))
              (1 (POST) OR ("вай") OR ("лыс") OR ("выжы") OR ("ул") OR ("выл") OR ("котыр") OR ("йыл") OR ("нюк") OR ("шудыны") OR ("сылыны") OR ("будыны") OR ("кизьыны") OR ("кидыс"));
SELECT (N) IF (0 ("<кыз>"i) OR ("<кызэ>"i))
              (-1 (ADJ) OR (attr_o) OR ("<ар>"i) OR ("<чебермам>"i) OR ("<чеберъям>"i) OR ("<будэм>"i) OR ("<будӥсь>"i));

# индэ: discourse particle vs. sky.ACC.2SG
SELECT (PART) IF (0 ("индэ"i))
                 (*-1 VFinPred BARRIER NotClitics)
                 (NEGATE 1 VTr);

# ог: NUM vs. PREP
SELECT (NUM) IF (0 ("ог"))
                (NEGATE 1 (NUM));
